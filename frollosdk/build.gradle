apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka-android'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 23 // Android 6.0 (Marshmallow)
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        // TODO: Signing
        // TODO: Configure proguard - debugging crash logs might be a problem due to obfuscation
        // TODO: Make sure to test release version after obfuscation, especially serializable classes
        release {
            debuggable false
            //minifyEnabled true
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //consumerProguardFiles 'lib-proguard-rules.txt'
        }
        debug {
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'

        String sharedTestDir = 'src/sharedTest/kotlin'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
    kapt {
        arguments {
            // Print schemas for each version of room database
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }
    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
        reportUndocumented = true
        includeNonPublic = false
        skipDeprecated = false
        skipEmptyPackages = true

        includes = ['packages.md','extras.md']

        packageOptions {
            prefix = "us.frollo.frollosdk.data.local"
            suppress = true
        }
        packageOptions {
            prefix = "us.frollo.frollosdk.keystore"
            suppress = true
        }
        packageOptions {
            prefix = "us.frollo.frollosdk.data.remote"
            suppress = true
        }
        packageOptions {
            prefix = "us.frollo.frollosdk.preferences"
            suppress = true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Testing
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:2.19.0"
    testImplementation "com.squareup.okhttp3:mockwebserver:3.8.0"
    androidTestImplementation "androidx.test:runner:1.1.1"
    androidTestImplementation "androidx.test:rules:1.1.1"
    androidTestImplementation "androidx.test:core:1.1.0"
    androidTestImplementation "androidx.arch.core:core-testing:2.0.0"
    androidTestImplementation "org.mockito:mockito-android:2.19.0"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:3.8.0"
    androidTestImplementation "com.squareup.retrofit2:retrofit-mock:2.3.0"
    androidTestImplementation "com.jraska.livedata:testing:0.6.0"
    androidTestImplementation "com.jraska.livedata:testing-ktx:0.6.0"

    // JavaX Annotations
    compileOnly "org.glassfish:javax.annotation:10.0-b28"

    // Android KTX
    implementation "androidx.core:core-ktx:1.0.1"

    // Android Arch Components
    implementation "androidx.lifecycle:lifecycle-livedata:2.0.0"
    implementation "androidx.room:room-runtime:2.0.0"
    kapt "androidx.room:room-compiler:2.0.0"

    // Anko (https://github.com/Kotlin/anko)
    implementation "org.jetbrains.anko:anko:0.10.7"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.3.0"
    implementation "com.squareup.retrofit2:converter-gson:2.3.0"

    // ThreeTenABP - Date-Time
    implementation "com.jakewharton.threetenabp:threetenabp:1.1.1"

    // Android Debug SDKDatabase
    debugImplementation "com.amitshekhar.android:debug-db:1.0.4"

    // Cryptography - SpongyCastle
    implementation "com.madgag.spongycastle:core:1.58.0.0"
    implementation "com.madgag.spongycastle:prov:1.58.0.0"
    implementation "com.madgag.spongycastle:bcpkix-jdk15on:1.58.0.0"
    implementation "com.madgag.spongycastle:bcpg-jdk15on:1.58.0.0"
}
repositories {
    mavenCentral()
}
